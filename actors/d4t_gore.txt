// Gore system from D4T
// https://www.moddb.com/mods/death-foretold

//--------------------------------------------------------------------
//--------------------------------------------------------------------
//	Blood generated by shooting
//--------------------------------------------------------------------
//--------------------------------------------------------------------
Actor D4TBlood replaces Blood
{
	+NOTONAUTOMAP
	+NOINTERACTION
	+CLIENTSIDEONLY
	Scale 0.75
	States
	{
	Spawn:
		X057 AA 0 NODELAY
		X057 AA 0 A_SpawnItemEx("BloodParticle",0,0,height,frandom(4,-4),frandom(4,-4),frandom(0,2),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,128)
		X057 AAA 0 A_SpawnItemEx("BloodParticleSmall",0,0,height,frandom(4,-4),frandom(4,-4),frandom(0,2),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,64)
		X057 A 0 A_SpawnItemEx("BloodSpray1",0,0,height/2,frandom(4,-4),frandom(4,-4),frandom(2,5),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,190)
		X057 AA 0 A_SpawnItemEx("BloodSpray2",0,0,height/2,frandom(4,-4),frandom(4,-4),frandom(3,8),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,150)
		X057 A 0 A_SpawnItemEx("NewMeat",0,0,height/2,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,170)
		X057 A 0 A_Jump(256,"Type07","Type08","Type09","TypeA7","TypeA8","TypeA9","TypeB7","TypeB8","TypeB9","TypeC7","TypeC8","TypeC9")
		
	Type07:		X057 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	Type08:		X058 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	Type09:		X059 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeA7:		XA57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeA8:		XA58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeA9:		XA59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeB7:		XB57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeB8:		XB58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeB9:		XB59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeC7:		XC57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeC8:		XC58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeC9:		XC59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	
	Form1Ender:
		"####" A 1
		"####" B 1 A_Stop
		"####" CDEFGHIJ 1
		"####" KLMNOPQRS 1 A_FadeOut
		Stop
	Form2Ender:
		"####" A 1
		"####" B 1 A_Stop
		"####" CDEFGHI 1
		"####" JKLMNOPQR 1 A_FadeOut
		Stop
	Form3Ender:
		"####" A 1
		"####" B 1 A_Stop
		"####" CD 1
		"####" EFGHIJKLM 1 A_FadeOut
		Stop
	}
}


//--------------------------------------------------------------------
//--------------------------------------------------------------------
//	Generic Blood particle for blood trails
//--------------------------------------------------------------------
//--------------------------------------------------------------------

Actor BloodGibDropper
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CheckSight("Simple")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("NewMeat",0,0,0,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 	A_SpawnItemEx("BloodCloud",random(15,-15),random(15,-15),random(15,-15),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		Stop
	Simple:
		TNT1 AAAA 0 A_SpawnItemEx("NewMeat",0,0,0,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 10 A_SpawnItemEx("BloodCloud",random(15,-15),random(15,-15),random(15,-15),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		Stop
	}
}


Actor BloodGibDropperBig
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOTONAUTOMAP
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CheckSight("Simple")
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("NewMeat",0,0,0,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 AAA 0 	A_SpawnItemEx("BloodCloudBig",random(15,-15),random(15,-15),random(15,-15),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		Stop
	Simple:
		TNT1 AAAA 0 A_SpawnItemEx("NewMeat",0,0,0,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 10 A_SpawnItemEx("BloodCloudBig",random(15,-15),random(15,-15),random(15,-15),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		Stop
	}
}


Actor ExtraGibbletsDropper
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAA 0 NODELAY A_SpawnItemEx("NewMeat",0,0,0,frandom(4,-4),frandom(4,-4),frandom(3,10),random(1,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 10 
		Stop
	}
}

//--------------------------------
Actor BloodGib1		
{
	+CLIENTSIDEONLY
	-NOLIFTDROP
	-SOLID
	+DROPOFF
	+NOTELEPORT
	+NOBLOCKMONST
	+CORPSE
	+NOTONAUTOMAP
	Scale 0.6
	Radius 6
	Height 5
	Gravity 0.65
	States
	{
	Spawn:
		ANYG A 0 NODELAY
		"####" "#" 0 A_QueueCorpse
		ANYG A 6 
		ANYG BCDA 5 A_SpawnItemEx("BloodParticle",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		Goto Spawn+1
	Crash:
		ANYG E 450 A_NoBlocking
		ANYG E 1 A_FadeOut
		Wait
	}
}

//--------------------------------
Actor BloodGib2 : BloodGib1		//eye
{
	Scale 0.4
	Speed 7
	States
	{
	Spawn:
		ANYG F 0 NODELAY 
		"####" "#" 0 A_QueueCorpse
		ANYG F 6 
		ANYG GHIF 5 A_SpawnItemEx("BloodParticle",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		Goto Spawn+1
	Crash:
		"####" "#" 0 A_QueueCorpse
		ANYG J 450 A_NoBlocking
		ANYG J 1 A_FadeOut
		Wait
	}
}
//--------------------------------
Actor BloodGib3 : BloodGib1		//liver
{
	Scale 0.6
	Speed 7
	States
	{
	Spawn:
		ANYG K 0 NODELAY 
		"####" "#" 0 A_QueueCorpse
		ANYG K 6 
		ANYG LMNK 5 A_SpawnItemEx("BloodParticleSmall",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		Goto Spawn+1
	Crash:
		"####" "#" 0 A_QueueCorpse
		ANYG O 450 A_NoBlocking
		ANYG O 1 A_FadeOut
		Wait
	}
}
//--------------------------------
Actor BloodGib4 : BloodGib1		//bloodthing
{
	Scale 0.6
	Speed 7
	States
	{
	Spawn:
		ANYG P 0 NODELAY 
		"####" "#" 0 A_QueueCorpse
		ANYG P 6 
		ANYG QRSP 5 A_SpawnItemEx("BloodParticle",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		Goto Spawn+1
	Crash:
		"####" "#" 0 A_QueueCorpse
		ANYG T 450 A_NoBlocking
		ANYG T 1 A_FadeOut
		Wait
	}
}
Actor BloodSpray1 : BloodGib1
{
	+MISSILE
	decal "BloodSmear"
	
	States
	{
	Spawn:
		TNT1 A 6 NODELAY
		TNT1 A 5 A_SpawnItemEx("BloodParticle",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		wait
	Crash:
		TNT1 A 1
		stop
	}
}
Actor BloodSpray2 : BloodSpray1
{
	decal "BloodSmear"
	
	States
	{
	Spawn:
		TNT1 A 6 NODELAY
		TNT1 A 5 A_SpawnItemEx("BloodParticleSmall",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,1)
		wait
	}
}
//--------------------------------
Actor BloodCloud
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOTONAUTOMAP
	Scale 0.45
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(255,"T1","T2","T3","T4","T5","T6","T7","T8")
	T1:
		X050 G 0	
		Goto PlayOut
	T2:
		X050 H 0	
		Goto PlayOut
	T3:
		X050 C 0	
		Goto PlayOut
	T4:
		X050 D 0	
		Goto PlayOut
	T5:
		X050 E 0	
		Goto PlayOut
	T6:
		X050 F 0	
		Goto PlayOut
	T7:
		X050 E 0	
		Goto PlayOut
	T8:
		X050 F 0	
		Goto PlayOut
	PlayOut:
		"####" "#" 1 A_SetScale(scalex+0.025)
		"####" "#" 0 A_FadeOut(0.04)
		Loop
	}
}
Actor BloodCloudMediumBig : BloodCloud
{	Scale 0.6	}

Actor BloodCloudBig : BloodCloud
{	Scale 0.8	}


//===========================================================
Actor NewMeat replaces Meat
{
	+MISSILE
	+CLIENTSIDEONLY
	var int user_time;
	var int user_t;
	+THRUACTORS
	-NOLIFTDROP
	+DROPOFF
	+NOTELEPORT
	+NOTONAUTOMAP
	-NOGRAVITY
	+NOBLOCKMONST
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	BounceType "Doom"
	BounceCount 2
	BounceFactor 0.4
	Radius 6
	Height 5
	Gravity 0.65
	decal "BloodSplat"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_QueueCorpse
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20)
		MEAT ABCDEFGHIJLKMNOPQRST 0 A_JumpIf(1 == 1,"Play")
	Play:
		"####" "#" 2 A_SpawnItemEx("BloodParticle",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION,128)
		"####" "#" 0 A_CheckFloor("md1")
		Loop
		
	// when the gib crashes:
	md1: 
	Death:
		"####" "#" 0 A_Jump(128,2)
		"####" "#" 0 A_PlaySound("Doom4/Gore/Bounce",CHAN_AUTO,0.5,0,1.5)
		"####" "##" 0 A_QueueCorpse
		"####" "#" 35 A_JumpIf(random(0,1),1)
		wait
		"####" "#" 1 A_FadeOut(0.02)
		Wait
	}
}

//--------------------------------------------------------------------
//--------------------------------------------------------------------
//	Generic Blood particle for blood trails
//--------------------------------------------------------------------
//--------------------------------------------------------------------
Actor BloodParticle
{
	+NOINTERACTION
	Scale 0.75
	+NOTONAUTOMAP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		X057 A 0 NODELAY A_Jump(256,"Type07","Type08","Type09","TypeA7","TypeA8","TypeA9","TypeB7","TypeB8","TypeB9","TypeC7","TypeC8","TypeC9")
		
	Type07:		X057 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	Type08:		X058 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	Type09:		X059 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeA7:		XA57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeA8:		XA58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeA9:		XA59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeB7:		XB57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeB8:		XB58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeB9:		XB59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	TypeC7:		XC57 A 0 A_Jump(256,"Form1Ender") Goto Form1Ender
	TypeC8:		XC58 A 0 A_Jump(256,"Form2Ender") Goto Form2Ender
	TypeC9:		XC59 A 0 A_Jump(256,"Form3Ender") Goto Form3Ender
	
	Form1Ender:
		"####" ABCDEFGHIJ 1
		"####" KLMNOPQRS 1 A_FadeOut
		Stop
	Form2Ender:
		"####" ABCDEFGHI 1
		"####" JKLMNOPQR 1 A_FadeOut
		Stop
	Form3Ender:
		"####" ABCD 1
		"####" EFGHIJKLM 1 A_FadeOut
		Stop
	}
}

Actor BloodParticleSmall : BloodParticle
{
	Scale 0.65
}



Actor NoBloodPuff
{
	+NOINTERACTION
	+BLOODLESSIMPACT
	+PAINLESS
	+NOTONAUTOMAP
	States
	{
	Spawn:
		TNT1 A 3
		Stop
	}
}