
actor RoboFist : Weapon replaces Fist
{
	Weapon.SelectionOrder 2600
	Weapon.Kickback 10
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+NOEXTREMEDEATH
	+NOALERT
	+inventory.undroppable
	Obituary "%o was apprehended."
	Weapon.SlotNumber 1
	+Weapon.NoHandSwitch
  
	States
	{
	Ready:
		TNT1 A 0
		RPUN A 1 A_WeaponReady
		loop
	Deselect:
		RPUN A 1 A_Lower(160)
		loop
	Select: 
		RPUN A 1 A_Raise(160)
		loop
	Fire:
		RPUN BC 2
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, 2)
		"####" D 1 A_CustomPunch (15,0,0,"EnhancedPunchPuff",72,0,0,"ArmorBonus","melee/swing")
		"####" # 0 A_Jump(256, 2)
		"####" D 1 A_CustomPunch (15,0,0,"RoboPunchPuff",72,0,0,"ArmorBonus","robopunch/smack")
		"####" EF 2
		"####" A 3
		"####" A 0 A_Refire
		goto Ready
	AltFire:
		RPUN G 3 A_PlaySound("robospike/out",CHAN_WEAPON)
		RPUN H 3
		"####" I 7
		"####" JK 2
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, 2)
		"####" L 7 A_CustomPunch (100,0,0,"EnhancedSpikePuff",72,0,0,"ArmorBonus","robospike/hit")
		"####" # 0 A_Jump(256, 2)
		"####" L 7 A_CustomPunch (100,0,0,"RoboSpikePuff",72,0,0,"ArmorBonus","robospike/hit")
		"####" MN 2
		"####" A 9
		"####" A 0 A_Refire
		goto Ready
	Spawn:
		RPUN P -1
		Stop
	}
}

actor RoboOffhandFist : RoboFist
{
	+Weapon.OffhandWeapon
	+Weapon.NoHandSwitch
}

ACTOR RoboPunchPuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +NOEXTREMEDEATH
  +PUFFONACTORS
  +NODECAL
  RenderStyle Translucent
  Alpha 0.6
  VSpeed 1
  States
  {
  Spawn:
    PUFF ABCD 4
    Stop
  }
}

ACTOR RoboSpikePuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +NOEXTREMEDEATH
  +PUFFONACTORS
  +NODECAL
  RenderStyle Translucent
  Alpha 0.6
  VSpeed 1
  DamageType "SpikeStab"
  States
  {
  Spawn:
    PUFF ABCD 4
    Stop
  }
}