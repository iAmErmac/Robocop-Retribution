
ACTOR Ed209Gun : weapon replaces Chaingun 12331
{
	//$Category RoboGuns
	Weapon.SelectionOrder 1
	Weapon.Ammotype "20mmSlug"
	Weapon.Ammouse 1
	Weapon.Ammogive 25
	Weapon.SlotNumber 4
	decal BulletChip
	Inventory.Pickupmessage "ED-209 Autocannon!"
	decal none
	
	States
	{
	Ready:
		SHRP A 1 A_WeaponReady
		loop
	Select:
		SHRP C 1 A_RAISE
		Loop
	Deselect:
		SHRP C 1 A_Lower
		Loop
	Fire:
		SHRP A 0 A_GunFlash
		SHRP B 0 Bright A_PlaySound("ed209/shoot")
		SHRP BC 1 Bright
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, 3)
		SHRP D 1 Bright A_FireBullets(5,2,2,10,"EnMachinegunPuff")
		"####" # 0 A_Jump(256, 2)
		SHRP D 1 Bright A_FireBullets(5,2,2,10,"30calPuff")
		SHRP EF 1
		SHRP A 0 A_Refire
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, "ready")
		"####" # 0 A_FireProjectile("ChaingunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		Goto ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_tracers")==false, "GunBarrelFlash")
		TNT1 A 0 A_FireProjectile("BulletTracer",random(-1,1),0,0,0,FPF_NOAUTOAIM,random(-1,1))
		TNT1 A 0 A_FireProjectile("CLBulletTracer",random(-1,1),0,0,0,FPF_NOAUTOAIM,random(-1,1))
	GunBarrelFlash:
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_weapons_mf")==true, "MuzzleFlash")
		TNT1 A 2 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		TNT1 A 1 A_Light0
		Stop
	MuzzleFlash:
		CGMF AB 1 Bright A_Light2
		CGMF C 1 Bright A_Light1
		CGMF D 1 A_Light0
		Stop
	Spawn:
		SHRP P 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==1, "Spawn3D")
		SHRP P 1
		loop
	Spawn3D:
		SHPP A 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==0, "Spawn")
		SHPP A 1 Bright
		loop
	}
}

ACTOR 20mmSlug : Ammo 12332
{
	//$Category Ammunition
	Inventory.Amount 15
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 25
	Ammo.BackpackMaxAmount 200
	Inventory.PickupMessage "A handful of 20mm Autocannon Rounds."
	Inventory.Icon AMAUA0
	
	States
	{
	Spawn:
		AMAU A 1
		Loop
	}
}

ACTOR 30calPuff : BulletPuff
{
	decal bulletchip
	Scale 1.25
	+EXTREMEDEATH
}