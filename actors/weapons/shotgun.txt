
actor RoboShotgun : RoboCopWeapon replaces Shotgun 12326
{
	//$Category RoboGuns
	Weapon.SelectionOrder 3
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Weapon.slotNumber 3
	decal BulletChip
	Inventory.PickupMessage "Tactical Shotgun!"
	AttackSound "weapons/shotfire"
	Obituary "%o is sucking on buckshot."
	decal none
	
	States
	{
	Ready:
		RSHG A 0 A_ReplaceSprite("rvw_weapon_mode", "RSHH")
		"####" A 1 A_WeaponReady
		Loop
	Deselect:
		"####" A 1 A_Lower(160)
		Loop
	Select:
		RSHG A 0 A_ReplaceSprite("rvw_weapon_mode", "RSHH")
		"####" A 1 A_Raise(160)
		Loop
	Fire:
		"####" B 0 A_GunFlash
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, 4)
		"####" # 0 A_FireBullets (7, 2, 5, 7, "EnShotPuff")
		"####" # 0 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		"####" # 0 A_Jump(256, 2)
		"####" # 0 A_FireBullets (7, 2, 5, 7, "ShotPuff")
		"####" BC 4 Bright
		"####" DE 3
		"####" FGH 1
		"####" A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_tracers")==false, 6)
		TNT1 AAAAA 0 A_FireProjectile("BulletTracer",random(-3,3),0,0,0,FPF_NOAUTOAIM,random(-1,1))
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_weapons_mf")==true, "MuzzleFlash")
		TNT1 A 4 Bright A_Light2
		TNT1 A 4 Bright A_Light1
		TNT1 A 2 A_Light0
		Stop
	MuzzleFlash:
		SHMF A 2 Bright A_Light2
		TNT1 A 2 A_Light2
		TNT1 A 4 A_Light1
		TNT1 A 2 A_Light0
		Stop
	Spawn:
		RSHG P 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==1, "Spawn3D")
		RSHG P 1
		loop
	Spawn3D:
		RSHG A 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==0, "Spawn")
		RSHP A 1 Bright
		loop
	Dummy:
		RSHH ABCDEFGH 1
		Stop
	}
}

ACTOR ShotPuff : BulletPuff
{
  DamageType ShotgunPellet
  decal ShotChip
}