
actor RoboShotgun : Weapon replaces Shotgun 12326
{
	//$Category RoboGuns
	Weapon.SelectionOrder 3
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Weapon.slotNumber 3
	decal BulletChip
	Inventory.PickupMessage "Tactical Shotgun!"
	AttackSound "weapons/shotfire"
	Obituary "%o is sucking on buckshot."
	decal none
	
	States
	{
	Ready:
		RSHG A 1 A_WeaponReady
		Loop
	Deselect:
		RSHG A 1 A_Lower
		Loop
	Select:
		RSHG A 1 A_Raise
		Loop
	Fire:
		RSHG A 0 A_GunFlash
		"####" # 0 A_JumpIf(GetCVAR("rvw_weapons")==0, 4)
		"####" # 0 A_FireBullets (7, 2, 5, 7, "EnShotPuff")
		"####" # 0 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		"####" # 0 A_Jump(256, 2)
		"####" # 0 A_FireBullets (7, 2, 5, 7, "ShotPuff")
		RSHG BC 4 Bright
		RSHG DED 3
		RSHG A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_tracers")==false, "GunBarrelFlash")
		TNT1 AAAAA 0 A_FireProjectile("BulletTracer",random(-3,3),0,0,0,FPF_NOAUTOAIM,random(-1,1))
	GunBarrelFlash:
		TNT1 A 0 A_JumpIf(GetCVAR("rvw_weapons_mf")==true, "MuzzleFlash")
		TNT1 A 4 Bright A_Light2
		TNT1 A 4 Bright A_Light1
		TNT1 A 2 A_Light0
		Stop
	MuzzleFlash:
		SHMF A 4 Bright A_Light2
		SHMF B 4 Bright A_Light1
		TNT1 A 2 A_Light0
		Stop
	Spawn:
		RSHG P 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==1, "Spawn3D")
		RSHG P 1
		loop
	Spawn3D:
		RSHG A 0 A_JumpIf(GetCVAR("rvw_pickupmodel")==0, "Spawn")
		RSHP A 1 Bright
		loop
	}
}

ACTOR ShotPuff : BulletPuff
{
  DamageType ShotgunPellet
  decal ShotChip
}